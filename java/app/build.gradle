apply plugin: 'com.android.application'
apply plugin: 'fullstory'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

fullstory {
    // add your "org" in local.properties, or set it here directly
    org properties.getProperty('org')
    if(org == null) logger.warn('ORG ID is empty, will not record! Please set org in local.properties!')

    // enable variants that you want to be instrumented by FS ( default to 'release' if not set )
    if(properties.getProperty('enabledVariants') != null) {
        enabledVariants properties.getProperty('enabledVariants')
    }

    // please do not set server value in local.properties
    if(properties.getProperty('server') != null){
        server properties.getProperty('server')
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "com.fullstorydev.shoppedemo"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintlayout_version"
    implementation "androidx.navigation:navigation-fragment:$rootProject.nav_version"
    implementation "androidx.navigation:navigation-ui:$rootProject.nav_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycle_version"
    implementation "androidx.room:room-runtime:$rootProject.room_version"
    annotationProcessor "androidx.room:room-compiler:$rootProject.room_version"
    implementation "androidx.multidex:multidex:$rootProject.multidex_version"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp_version"
    implementation "com.google.code.gson:gson:$rootProject.gson_version"
    implementation "com.google.android.material:material:$rootProject.material_version"
    implementation "com.github.bumptech.glide:glide:$rootProject.glide_version"

    testImplementation "junit:junit:$rootProject.junit_version"
    testImplementation "androidx.room:room-testing:$rootProject.room_version"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espresso_version"
}